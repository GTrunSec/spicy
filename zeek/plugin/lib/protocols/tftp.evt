protocol analyzer spicy::TFTP over UDP:
    parse with TFTP::Message,   # entry point for parsing TFTP payload
    port 69/udp;                # use analyzer for sessions on UDP port 69

import TFTP;

on TFTP::Request if ( is_read )  -> event tftp::read_request($conn, $is_orig, self.fname, self.mode);
on TFTP::Request if ( ! is_read ) -> event tftp::write_request($conn, $is_orig, self.fname, self.mode);

on TFTP::Data  -> event tftp::data($conn, $is_orig, self.num, self.data);
on TFTP::Ack   -> event tftp::ack($conn, $is_orig, self.num);
on TFTP::Error -> event tftp::error($conn, $is_orig, self.code, self.msg);
